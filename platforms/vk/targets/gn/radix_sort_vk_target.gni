# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/compute/tools/target_archive/gn/target_archive.gni")

#
# Declare Radix Sort Vulkan target.
#
template("radix_sort_vk_target") {
  #
  # Where is the radix sort library?
  #
  _radix_sort_vk_dir = "//src/graphics/lib/compute/radix_sort/platforms/vk"

  #
  # struct_sources
  #
  struct_sources = [
    "${_radix_sort_vk_dir}/target.h",
    "${_radix_sort_vk_dir}/target_requirements.h",
    "config.h",
    "header.c",
  ]

  if (defined(invoker.struct_sources)) {
    struct_sources += invoker.struct_sources
  }

  #
  # struct_include_dirs
  #
  struct_include_dirs = [ _radix_sort_vk_dir ]

  if (defined(invoker.struct_include_dirs)) {
    struct_include_dirs += invoker.struct_include_dirs
  }

  #
  # shader_sources
  #
  # ***********************************************
  # *** shaders must remain in the listed order ***
  # ***********************************************
  #
  # NOSORT
  shader_sources = [
    "${_radix_sort_vk_dir}/shaders/init.comp",
    "${_radix_sort_vk_dir}/shaders/fill.comp",
    "${_radix_sort_vk_dir}/shaders/histogram.comp",
    "${_radix_sort_vk_dir}/shaders/prefix.comp",
    "${_radix_sort_vk_dir}/shaders/scatter_0_even.comp",
    "${_radix_sort_vk_dir}/shaders/scatter_0_odd.comp",
  ]

  assert(defined(invoker.keyval_dwords), "Define invoker.keyval_dwords")

  if (invoker.keyval_dwords == 2) {
    shader_sources += [
      "${_radix_sort_vk_dir}/shaders/scatter_1_even.comp",
      "${_radix_sort_vk_dir}/shaders/scatter_1_odd.comp",
    ]
  } else if (invoker.keyval_dwords > 2) {
    assert(false, "invoker.keyval_dwords value not supported")
  }

  if (defined(invoker.shader_sources)) {
    shader_sources += invoker.shader_sources
  }

  #
  # shader_inputs
  #
  shader_inputs = [
    "config.h",
    "${_radix_sort_vk_dir}/shaders/bufref.h",
    "${_radix_sort_vk_dir}/shaders/prefix.h",
    "${_radix_sort_vk_dir}/shaders/prefix_limits.h",
    "${_radix_sort_vk_dir}/shaders/push.h",
    "${_radix_sort_vk_dir}/shaders/scatter.glsl",
  ]

  if (defined(invoker.shader_inputs)) {
    shader_inputs += invoker.shader_inputs
  }

  #
  # shader_include_dirs
  #
  shader_include_dirs = [
    ".",
    "${_radix_sort_vk_dir}/shaders",
  ]

  if (defined(invoker.shader_include_dirs)) {
    shader_include_dirs += invoker.shader_include_dirs
  }

  #
  # defines
  #
  if (defined(invoker.defines)) {
    defines = invoker.defines
  }

  #
  # invoke customized target archive template
  #
  forward_variables_from(invoker,
                         [
                           "is_sources",
                           "is_resource",
                           "is_linkable",
                         ])

  assert(defined(is_sources) || defined(is_resource) || defined(is_linkable),
         "Define either `is_sources` or `is_resource` or `is_linkable` boolean")

  if (defined(is_sources) && is_sources) {
    target_archive(target_name) {
      forward_variables_from(invoker,
                             [
                               "struct_sources",
                               "struct_include_dirs",
                               "shader_sources",
                               "shader_inputs",
                               "shader_include_dirs",
                               "defines",
                               "skip_spirv_opt",
                             ])
      if (!defined(skip_spirv_opt)) {
        skip_spirv_opt = false
      }
      gen_debug_shaders = true
    }
  } else if (defined(is_resource) && is_resource) {
    target_archive_resource(target_name) {
      forward_variables_from(invoker,
                             [
                               "struct_sources",
                               "struct_include_dirs",
                               "shader_sources",
                               "shader_inputs",
                               "shader_include_dirs",
                               "defines",
                               "skip_spirv_opt",
                             ])
      if (!defined(skip_spirv_opt)) {
        skip_spirv_opt = false
      }
      gen_debug_shaders = true
    }
  } else if (defined(is_linkable) && is_linkable) {
    target_archive_linkable(target_name) {
      forward_variables_from(invoker,
                             [
                               "struct_sources",
                               "struct_include_dirs",
                               "shader_sources",
                               "shader_inputs",
                               "shader_include_dirs",
                               "defines",
                               "skip_spirv_opt",
                             ])
      if (!defined(skip_spirv_opt)) {
        skip_spirv_opt = false
      }
      gen_debug_shaders = true
    }
  }
}

#
# Sources: 32-bit keyval target
#
template("radix_sort_vk_target_u32_outputs") {
  radix_sort_vk_target(target_name) {
    is_sources = true
    keyval_dwords = 1
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}

#
# Fuchsia resource: 32-bit keyval target
#
template("radix_sort_vk_target_u32_resource") {
  radix_sort_vk_target(target_name) {
    is_resource = true
    keyval_dwords = 1
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}

#
# Linkable: 32-bit keyval target
#
template("radix_sort_vk_target_u32_linkable") {
  radix_sort_vk_target(target_name) {
    is_linkable = true
    keyval_dwords = 1
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}

#
# Sources: 64-bit keyval target
#
template("radix_sort_vk_target_u64_outputs") {
  radix_sort_vk_target(target_name) {
    is_sources = true
    keyval_dwords = 2
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}

#
# Fuchsia resource: 64-bit keyval target
#
template("radix_sort_vk_target_u64_resource") {
  radix_sort_vk_target(target_name) {
    is_resource = true
    keyval_dwords = 2
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}

#
# Linkable: 64-bit keyval target
#
template("radix_sort_vk_target_u64_linkable") {
  radix_sort_vk_target(target_name) {
    is_linkable = true
    keyval_dwords = 2
    forward_variables_from(invoker,
                           [
                             "struct_sources",
                             "struct_include_dirs",
                             "shader_sources",
                             "shader_inputs",
                             "shader_include_dirs",
                             "defines",
                             "skip_spirv_opt",
                           ])
  }
}
